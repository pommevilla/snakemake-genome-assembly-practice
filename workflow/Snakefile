configfile: "config/config.yml"
include: "rules/common.smk"
include: "rules/download_sras.smk"
include: "rules/assembly.smk"
include: "rules/metrics.smk"

localrules: all, generate_snakemake_dag, clean_all, download_sra, dump_fastqs, fastp_qc, collect_quast_metrics, collect_genomes

####### Target rules #######
rule all:
    input:
        "results/snakemake_dag.png",
        expand(
            "{spades_output_dir}/{sra}/spades_output/scaffolds.fasta", 
            spades_output_dir=config['sra_download_directory'],
            sra=SRA_LIST
        ),
        expand(
            "{final_genome_directory}/{sra}_scaffolds.fasta",
            final_genome_directory=config['final_genome_directory'],
            sra=SRA_LIST
        ),
        f"{config['final_metrics_directory']}/quast_metrics.txt",
        expand(
            "{final_metrics_directory}/checkm2",
            final_metrics_directory=config['final_metrics_directory'],
        )

####### Meta rules #######
rule generate_snakemake_dag:
    output:
        "results/snakemake_dag.png"
    log:
        err="logs/snakemake_dag.err",
        out="logs/snakemake_dag.out"
    shell:
        "snakemake --rulegraph | dot -Tpng > {output}"

rule clean_all:
    log:
        err="logs/clean_all.err",
        out="logs/clean_all.out"
    params:
        sra_download_directory=config["sra_download_directory"]
    shell:
        """
        echo "Removing all logs..."
        rm -rf logs/*
        rm -rf .snakemake/log

        echo "Removing locks..."
        rm .snakemake/locks/*
        """


